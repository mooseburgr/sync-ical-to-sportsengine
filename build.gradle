plugins {
  id 'java'
  id "io.freefair.lombok" version "8.4"
  id "com.diffplug.spotless" version "6.23.2"
}

repositories {
  google()
  mavenCentral()
}
configurations {
    invoker
}

dependencies {
  implementation(platform("com.fasterxml.jackson:jackson-bom:2.17.2"))
  implementation 'com.fasterxml.jackson.core:jackson-databind'
  implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

  implementation 'org.apache.commons:commons-lang3:3.14.0'
  implementation 'org.slf4j:log4j-over-slf4j:2.0.9'

  // Every function needs this dependency to get the Functions Framework API.
  compileOnly 'com.google.cloud.functions:functions-framework-api:1.1.0'

  // To run function locally using Functions Framework's local invoker
  invoker 'com.google.cloud.functions.invoker:java-function-invoker:1.3.1'

  // These dependencies are only used by the tests.
  testImplementation(platform("org.junit:junit-bom:5.10.1"))
  testImplementation 'org.junit.jupiter:junit-jupiter-api'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testImplementation 'com.google.cloud.functions:functions-framework-api:1.1.0'
  testImplementation 'com.google.truth:truth:1.1.5'
  testImplementation 'org.mockito:mockito-junit-jupiter:5.8.0'

}

// Register a "runFunction" task to run the function locally
tasks.register("runFunction", JavaExec) {
  main = 'com.google.cloud.functions.invoker.runner.Invoker'
  classpath(configurations.invoker)
  inputs.files(configurations.runtimeClasspath, sourceSets.main.output)
  args(
    '--target', project.findProperty('run.functionTarget') ?: '',
    '--port', project.findProperty('run.port') ?: 8080
  )
  doFirst {
    args('--classpath', files(configurations.runtimeClasspath, sourceSets.main.output).asPath)
  }
}

test {
  useJUnitPlatform()
}

spotless {
  java {
    googleJavaFormat()
  }
}